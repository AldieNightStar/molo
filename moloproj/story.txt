$import api/api.txt

:myalert
    * window.alert("Hello from alert scene. I am used as function");

== Main chapter
:main
    == Now i am changing bg image
    .bg "res/bg.jpg"
    .print-letter "As you can see bg image changed. '.bg' command doing that"
    .print-letter "Look closer"
    .bgscale 2, 2
    
    == Let's change positions of the background
    == To do so we use '.bgpos' and '.bgscale'
    == To change the speed we use '.bgtransition'
    .bgpos "left"
    .print-letter "Left side", 1000
    .bgpos "right"
    .print-letter "Right side", 1000
    .wait 1000
    .bgpos "center"
    .bgscale 1, 1
    .print-letter "We done with background!"

    == Now let's swipe couple of times
    .clear
    .print-letter "Now let's try background swiping"

    .bgswipe "left", "right", 1000
    .print-letter "Left to Right"
    .wait 1000

    .bgswipe "top", "bottom", 1000
    .print-letter "Top to Bottom"
    .wait 1000
    
    .continue "Let's go next"

    == This text will wait for continue button
    .print-letter "Hi all and everything which am added to be it here", 3000
    .continue "Let's start with a new story"
    .continue "Write your new world with Molo"
    .clear
    .continue "Cleared screen, is better than polished"

    == This is simple command which allows you to goto by name
    == Alternatively you could write: * buttonX("Next chapter", "chapter2")
    .button "Next chapter", "chapter2"

    == This is js block code
    == $$variables are mapped to window.mvars['variables']
    == $$$scenes are mapped to window.mscenes['scenes']
    == Just a small syntactic sugar
    .js
        $$cnt = 0;
    .endjs

:chapter2
    == This is simple js button
    * await button("JS button", () => $$$myalert());

    == This is advanced button functionality
    .button "Advanced button", "", () => window.alert("Hello from advanced buttons")

    == This is advanced button but in JS
    * await buttonX("Advanced JS button", "", () => window.alert("Hello from JS advanced buttons"))

    == Let's add some interaction
    == For more javascript api details - refer to README.md
    * await button("Count: " + $$cnt, () => { $$cnt += 1; mgoto(); });

    == Example for using button. Do not forget to use 'await'
    .js
        for (let i = 0; i < 10; i++) {
            await button(`B${i}`, () => window.alert(`Count is: ${i}`));
        }
    .endjs